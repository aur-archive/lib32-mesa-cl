# Maintainer: PitBall

pkgbase=lib32-mesa-cl
pkgname=('lib32-ati-dri-cl' 'lib32-intel-dri-cl' 'lib32-nouveau-dri-cl' 'lib32-mesa-libcl' 'lib32-mesa-cl' 'lib32-mesa-libgl-cl')
pkgver=10.2.1
pkgrel=1
arch=('x86_64')
makedepends=('python2' 'lib32-libxml2' 'lib32-expat' 'lib32-libxvmc' 'glproto' 'lib32-libdrm' 'dri2proto'  'dri3proto' 'presentproto'  'lib32-libxxf86vm' 'lib32-libxdamage'
             'gcc-multilib' 'lib32-libxshmfence' 'lib32-wayland' 'lib32-elfutils' 'lib32-clang' 'lib32-systemd' 'libclc-svn' 'lib32-libvdpau')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=(#http://cgit.freedesktop.org/mesa/mesa/snapshot/mesa-${pkgver}.tar.gz
        ftp://ftp.freedesktop.org/pub/mesa/${pkgver/.0//}/MesaLib-${pkgver}.tar.bz2
        .AURINFO)
md5sums=('093f9b5d077e5f6061dcd7b01b7aa51a'
         SKIP)

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export CLANG="clang -m32"
  export LLVM_CONFIG=/usr/bin/llvm-config32
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/?esa-*
  # our automake is far too new for their build system :)
  autoreconf -vfi

  ./configure --enable-32-bit \
    --libdir=/usr/lib32 \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-dri-driverdir=/usr/lib32/xorg/modules/dri \
    --with-gallium-drivers=r300,r600,radeonsi,nouveau,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --with-egl-platforms=x11,drm,wayland \
    --with-llvm-shared-libs \
    --enable-r600-llvm-compiler \
    --enable-gallium-llvm \
    --enable-gallium-egl \
    --enable-gallium-gbm \
    --enable-gallium-osmesa \
    --enable-egl \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-dri3 \
    --enable-glx \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-opencl \
    --enable-xa \
    --enable-texture-float
  make

  install -d $srcdir/fakeinstall
  make DESTDIR=${srcdir}/fakeinstall install
}

package_lib32-ati-dri-cl() {
  pkgdesc="Mesa drivers for AMD/ATI Radeon (32-bit)"
  depends=("lib32-mesa-libgl-cl=${pkgver}" 'ati-dri-cl')
  optdepends=('lib32-libtxc_dxtn: S3 Texture Compressed support')
  provides=("lib32-ati-dri=${pkgver}")
  conflicts=('lib32-ati-dri')
  replaces=('lib32-ati-dri')

  install -m755 -d ${pkgdir}/usr/lib32/xorg/modules/dri
   mv -v ${srcdir}/fakeinstall/usr/lib32/xorg/modules/dri/r*_dri.so ${pkgdir}/usr/lib32/xorg/modules/dri/

  install -m755 -d ${pkgdir}/usr/lib32/gallium-pipe
  mv -v ${srcdir}/fakeinstall/usr/lib32/gallium-pipe/pipe_r* ${pkgdir}/usr/lib32/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/ati-dri"
  ln -s ati-dri "$pkgdir/usr/share/licenses/ati-dri/lib32-ati-dri"
}

package_lib32-intel-dri-cl() {
  depends=("lib32-mesa-libgl-cl=${pkgver}" 'intel-dri-cl')
  optdepends=('lib32-libtxc_dxtn: S3 Texture Compressed support')
  provides=("lib32-intel-dri=${pkgver}")
  conflicts=('lib32-intel-dri')
  replaces=('lib32-intel-dri')
  pkgdesc="Mesa DRI drivers for Intel (32-bit)"

  install -m755 -d ${pkgdir}/usr/lib32/xorg/modules/dri
  mv -v ${srcdir}/fakeinstall/usr/lib32/xorg/modules/dri/{i915,i965}_dri.so ${pkgdir}/usr/lib32/xorg/modules/dri/

  install -m755 -d "${pkgdir}/usr/share/licenses/intel-dri"
  ln -s intel-dri "$pkgdir/usr/share/licenses/intel-dri/lib32-intel-dri"
}

package_lib32-nouveau-dri-cl() {
  pkgdesc="Mesa drivers for Nouveau (32-bit)"
  depends=("lib32-mesa-libgl-cl=${pkgver}" 'nouveau-dri-cl')
  optdepends=('lib32-libtxc_dxtn: S3 Texture Compressed support')
  provides=("lib32-nouveau-dri=${pkgver}")
  conflicts=('lib32-nouveau-dri')
  replaces=('lib32-nouveau-dri')

  install -m755 -d ${pkgdir}/usr/lib32/xorg/modules/dri
  mv -v ${srcdir}/fakeinstall/usr/lib32/xorg/modules/dri/nouveau_{dri,vieux_dri}.so ${pkgdir}/usr/lib32/xorg/modules/dri/

  install -m755 -d ${pkgdir}/usr/lib32/gallium-pipe
  mv -v ${srcdir}/fakeinstall/usr/lib32/gallium-pipe/pipe_nouveau* ${pkgdir}/usr/lib32/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/nouveau-dri"
  ln -s nouveau-dri "$pkgdir/usr/share/licenses/nouveau-dri/lib32-nouveau-dri"
}
package_lib32-mesa-libcl() {
  pkgdesc="Mesa CL library (32-bit)"
  depends=("lib32-mesa-cl=${pkgver}" 'lib32-libffi' 'opencl-headers')
  provides=("lib32-libcl=${pkgver}")
  conflicts=('lib32-libcl')
  replaces=('lib32-libcl')
  install -m755 -d ${pkgdir}/usr/lib32
  mv -v ${srcdir}/fakeinstall/usr/lib32/libOpenCL*  ${pkgdir}/usr/lib32/
}

package_lib32-mesa-cl() {
  pkgdesc="an open-source implementation of the OpenGL specification (32-bit)"
  depends=('lib32-libdrm' 'lib32-libxxf86vm' 'lib32-libxdamage' 'libxshmfence' 'lib32-systemd' 'lib32-elfutils' 'lib32-llvm-libs' 'mesa-cl')
  provides=("lib32-mesa=${pkgver}" 'lib32-libglapi' 'lib32-osmesa' 'lib32-libgbm' 'lib32-libgles' 'lib32-libegl')
  conflicts=('lib32-mesa' 'lib32-libglapi' 'lib32-osmesa' 'lib32-libgbm' 'lib32-libgles' 'lib32-libegl')
  replaces=('lib32-mesa' 'lib32-libglapi' 'lib32-osmesa' 'lib32-libgbm' 'lib32-libgles' 'lib32-libegl')

  mv -v ${srcdir}/fakeinstall/* ${pkgdir}
  mv -v ${pkgdir}/usr/lib32/libGL.so.1.2.0 ${pkgdir}/usr/lib32/mesa-libGL.so.1.2.0
  mv -v ${pkgdir}/usr/lib32/libEGL.so.1.0.0 ${pkgdir}/usr/lib32/mesa-libEGL.so.1.0.0
  mv -v ${pkgdir}/usr/lib32/libGLESv1_CM.so.1.1.0 ${pkgdir}/usr/lib32/mesa-libGLESv1_CM.so.1.1.0
  mv -v ${pkgdir}/usr/lib32/libGLESv2.so.2.0.0 ${pkgdir}/usr/lib32/mesa-libGLESv2.so.2.0.0

  rm ${pkgdir}/usr/lib32/lib{,E}GL.so{,.1}
  rm ${pkgdir}/usr/lib32/libGLES*.so{,.1,.2}
  rm -r ${pkgdir}/etc
  rm -r ${pkgdir}/usr/include

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  ln -s mesa "$pkgdir/usr/share/licenses/mesa/lib32-mesa"
}

package_lib32-mesa-libgl-cl(){
  pkgdesc="Mesa 3-D graphics library (32-bit)"
  depends=("lib32-mesa-cl=${pkgver}")
  provides=("lib32-libgl=${pkgver}" "lib32-mesa-libgl=${pkgver}")
  conflicts=('lib32-mesa-libgl')
  replaces=('lib32-libgl' 'lib32-mesa-libgl')

  install -m755 -d ${pkgdir}/usr/lib32

  ln -s mesa-libGL.so.1.2.0 ${pkgdir}/usr/lib32/libGL.so
  ln -s mesa-libGL.so.1.2.0 ${pkgdir}/usr/lib32/libGL.so.1
  ln -s mesa-libGL.so.1.2.0 ${pkgdir}/usr/lib32/libGL.so.1.2.0
  ln -s mesa-libEGL.so.1.0.0        ${pkgdir}/usr/lib32/libEGL.so
  ln -s mesa-libEGL.so.1.0.0        ${pkgdir}/usr/lib32/libEGL.so.1
  ln -s mesa-libEGL.so.1.0.0        ${pkgdir}/usr/lib32/libEGL.so.1.0.0
  ln -s mesa-libGLESv1_CM.so.1.1.0  ${pkgdir}/usr/lib32/libGLESv1_CM.so
  ln -s mesa-libGLESv1_CM.so.1.1.0  ${pkgdir}/usr/lib32/libGLESv1_CM.so.1
  ln -s mesa-libGLESv1_CM.so.1.1.0  ${pkgdir}/usr/lib32/libGLESv1_CM.so.1.1.0
  ln -s mesa-libGLESv2.so.2.0.0     ${pkgdir}/usr/lib32/libGLESv2.so
  ln -s mesa-libGLESv2.so.2.0.0     ${pkgdir}/usr/lib32/libGLESv2.so.2
  ln -s mesa-libGLESv2.so.2.0.0     ${pkgdir}/usr/lib32/libGLESv2.so.2.0.0

  install -m755 -d "${pkgdir}/usr/share/licenses/libglapi"
  ln -s libglapi "${pkgdir}/usr/share/licenses/libglapi/lib32-libglapi"
}
